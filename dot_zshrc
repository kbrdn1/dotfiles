# -------------------------- Core ZSH Configuration --------------------------
# Enable Powerlevel10k instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
		source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Path configurations
export ZSH="$HOME/.oh-my-zsh"
export XDG_CONFIG_HOME="$HOME/.config"
export EDITOR="zed"
export USER_FOLDER=$(basename $HOME)

# Load Oh-My-Zsh and plugins
plugins=(macos git git-auto-fetch gh battery zsh-autosuggestions zsh-syntax-highlighting zsh-autosuggestions brew chezmoi composer docker docker-compose alias-finder bun eza fzf history laravel symfony npm perms pip qrcode ssh systemadmin thefuck vi-mode web-search)
eval "$(brew shellenv)"
source $ZSH/oh-my-zsh.sh
source $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh

# -------------------------- History Configuration --------------------------
HISTFILE=$HOME/.zhistory
SAVEHIST=1000
HISTSIZE=999
setopt share_history
setopt hist_expire_dups_first
setopt hist_ignore_dups
setopt hist_verify

bindkey '^[[A' history-search-backward
bindkey '^[[B' history-search-forward

# -------------------------- Plugin Configurations --------------------------
# Alias Finder
zstyle ':omz:plugins:alias-finder' autoload yes
zstyle ':omz:plugins:alias-finder' longer yes
zstyle ':omz:plugins:alias-finder' exact yes
zstyle ':omz:plugins:alias-finder' cheaper yes

# VI Mode
VI_MODE_SET_CURSOR=true
MODE_INDICATOR="%F{white}+%f"
INSERT_MODE_INDICATOR="%F{yellow}+%f"
PROMPT="$PROMPT\$(vi_mode_prompt_info)"
RPROMPT="\$(vi_mode_prompt_info)$RPROMPT"
VI_MODE_CURSOR_NORMAL=2
VI_MODE_CURSOR_VISUAL=6
VI_MODE_CURSOR_INSERT=6
VI_MODE_CURSOR_OPPEND=0

# -------------------------- Path Configuration --------------------------
export PATH="/Users/kbrdn1/.bun/bin:$PATH"
export PATH="/opt/homebrew/bin:$PATH"
export PATH="$HOME/bin:$PATH"
export PATH="$ASDF_DATA_DIR/shims:$PATH"
export PATH="/usr/local/opt/bison/bin:$PATH"
export PATH="/opt/homebrew/opt/bison/bin:$PATH"
export PATH="/usr/local/opt/pkg-config/bin:$PATH"

# -------------------------- Tool Configurations --------------------------
# Homebrew
export HOMEBREW_NO_AUTO_UPDATE=1

# ASDF
export ASDF_DATA_DIR="/opt/homebrew/opt/asdf/"
export ASDF_PYTHON_PATCH_URL="https://github.com/python/cpython/commit/8ea6353.patch?full_index=1"

# FZF Configuration
eval "$(fzf --zsh)"
export FZF_DEFAULT_COMMAND="fd --hidden --strip-cwd-prefix --exclude .git"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND="fd --type d --hidden --strip-cwd-prefix --exclude .git"
export FZF_CTRL_T_OPTS="--preview 'bat -n --color=always --line-range :500 {}'"
export FZF_ALT_C_OPTS="--preview 'eza --tree --color=always {} | head -200'"

# Bat
export BAT_THEME="Catppuccin Macchiato"

# -------------------------- Aliases --------------------------
# System
alias x="exit"
alias config="cd $XDG_CONFIG_HOME"
alias edit-config="$EDITOR $XDG_CONFIG_HOME"

# ZSH
alias reload-zsh="source ~/.zshrc"
alias edit-zsh="$EDITOR ~/.zshrc"

# Application Tools
alias py='python3'
alias python='/usr/bin/python3'
alias pa='php artisan'
alias laravel='php artisan'
alias a='adonis ace'
alias adonis='node ace'
alias yt='youtube'
alias gg='google'
alias ls="eza --color=always --long --git --no-filesize --icons=always --no-time --no-user --no-permissions --group-directories-first"
alias cd="z"
alias lg="lazygit"
alias lzd='lazydocker'

# GitHub Copilot
alias gcs='gh copilot suggest'
alias gce='gh copilot explain'
alias gcc='gh copilot config'
alias gca='gh copilot alias'

# Window Manager Services
alias reload-sketchybar="brew services restart sketchybar"
alias edit-sketchybar="$EDITOR $XDG_CONFIG_HOME/sketchybar"
alias reload-jankyborders="brew services restart borders"
alias reload-borders="brew services restart borders"
alias edit-jankyborders="$EDITOR $XDG_CONFIG_HOME/borders"
alias edit-borders="$EDITOR $XDG_CONFIG_HOME/borders"
alias reload-svim="brew services restart svim"
alias reload-yabai="yabai --restart-service"
alias edit-yabai="$EDITOR $XDG_CONFIG_HOME/yabai"
alias reload-skhd="skhd --restart-service"
alias edit-skhd="$EDITOR $XDG_CONFIG_HOME/skhd/skhdrc"

# Tmux
alias t="tmux -2"
alias reload-tmux="tmux source-file ~/.tmux.conf"
alias edit-tmux="$EDITOR ~/.tmux.conf"

# -------------------------- Functions --------------------------
# Yazi
function y() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	yazi "$@" --cwd-file="$tmp"
	if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		builtin cd -- "$cwd"
	fi
	rm -f -- "$tmp"
}

# FZF Functions
_fzf_compgen_path() {
		fd --hidden --exclude .git . "$1"
}

_fzf_compgen_dir () {
		fd --type d --hidden --exclude .git . "$1"
}

_fzf_comprun() {
		local command=$1
		shift

		case "$command" in
				cd) fzf --preview 'eza --tree --color=always {} | head -200' "$@" ;;
				export | unset) fzf --preview "eval 'echo \$' {}" "$@" ;;
				ssh) fzf --preview 'dig {}' "$@" ;;
				*) fzf --preview 'bat -n --color=always --line-range :500 {}' "$@" ;;
		esac
}

# Sketchybar Functions
function brew() {
		command brew "$@" 
		if [[ $* =~ "upgrade" ]] || [[ $* =~ "update" ]] || [[ $* =~ "outdated" ]]; then
				sketchybar --trigger brew_update
		fi
}

function margin () {
		if [ $1 = "on" ]; then
				yabai -m config top_padding 20
				sketchybar --animate sin 30 --bar margin=10 y_offset=10 corner_radius=9
		else
				yabai -m config top_padding 10
				sketchybar --animate sin 30 --bar margin=0 y_offset=0 corner_radius=0
		fi
}

function zen () {
		$XDG_CONFIG_HOME/sketchybar/plugins/zen.sh $1
}

function suyabai () {
		SHA256=$(shasum -a 256 /opt/homebrew/bin/yabai | awk "{print \$1;}")
		sudo sed -i '' -e 's/sha256:[[:alnum:]]*/sha256:'${SHA256}'/' /private/etc/sudoers.d/yabai
}

# -------------------------- Final Initializations --------------------------
# Load completions and tools
eval "$(zoxide init zsh)"
eval $(thefuck --alias)
eval $(thefuck --alias fk)

# Theme
source $(brew --prefix)/share/powerlevel10k/powerlevel10k.zsh-theme
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
alias edit-p10k="$EDITOR ~/.p10k.zsh"

# Start
neofetch
